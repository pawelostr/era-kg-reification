# GRAPH

PREFIX era: <http://data.europa.eu/949/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

select distinct ?id (substr(xsd:string(?op), 51) as ?opIRIend) (substr(xsd:string(?obp), 51) as ?obpIRIend) where { 
    graph ?g2 {
        ?op era:uopid ?id .
        FILTER (!regex(xsd:string(?op),".*/.{2}/EU.*","i")) .
    } .
    graph ?g3 { 	# secondary graph as validity dates may be different 
        ?obp era:uopid ?id .
        FILTER (regex(xsd:string(?obp),".*/.{2}/EU.*","i")) .
    } .
    {
        select ?id ?name (MAX(?start) as ?startDate) 
        where { 
            graph ?g {
                ?s era:uopid ?id .
                ?s era:opName ?name .
                FILTER (!regex(xsd:string(?s),".*/.{2}/EU.*","i")) .
            } .
            ?g era:validityStart ?start .
            ?g era:validityEnd ?end .
            filter(?start < "2024-01-01"^^xsd:date) .
            filter(?end > "2024-01-01"^^xsd:date) .

        } group by ?id ?name order by desc(?startDate)
    } .
}

